enum{left, right}; //player position
enum{player1, player2, na}; // won player
enum{to_diagonal_near, to_diagonal_far, to_adjacent_near, to_adjacent_far}; //player action

/**
The probability in this simulation is retrieved from BWF2018 Kento MOMOTA vs LIN Dan. https://www.youtube.com/watch?v=iGDcBeztMHY
In our context, player1 is Kento MOMOTA and player2 is LIN Dan.
*/

var winning_score = 4;
var won = na;
var serve_player = na;
var player1_Score = 0;
var player2_Score = 0;
var player1_position = right;
var player2_position = right;
var player1_Last_Action = to_diagonal_near;
var player2_Last_Action = to_diagonal_near;

WhoServe1st = []i:{player1,player2}@ TossCoin{serve_player = i} -> Skip;

// if no one reaches winning points, a new round start
NewRound = [player1_Score<winning_score && player2_Score<winning_score]inProgress{won = na} -> Play
		[] 
	   [player1_Score >= winning_score]player1_won{won = player1} -> Skip
	    []
	   [player2_Score >= winning_score]player2_won{won = player2} -> Skip;

// serve
Play = [serve_player == player1]player1_to_serve -> Player1_Serve
	[]
	[serve_player == player2]player2_to_serve -> Player2_Serve;

// serve from right if even scores
Player1_Serve = [player1_Score%2 == 0]evenScore{player1_position = right} -> Player1_Right_Serve
				 []
				[player1_Score%2 == 1]oddScore{player1_position = left} -> Player1_Left_Serve;

Player2_Serve = [player2_Score%2 == 0]evenScore{player2_position = right} -> Player2_Right_Serve
				 []
				[player2_Score%2 == 1]oddScore{player2_position = left} -> Player2_Left_Serve;

// serve state, serve can only go to diagonal side, A's left to B's left, A's right to B's right
Player1_Right_Serve = pcase {
	819: Low_Serve{player1_Last_Action = to_diagonal_near} -> Player2_Right_Near
	180: High_Serve{player1_Last_Action = to_diagonal_far} -> Player2_Right_Far	
	1: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

Player1_Left_Serve = pcase {
	9989: Low_Serve{player1_Last_Action = to_diagonal_near} -> Player2_Left_Near
	10: High_Serve{player1_Last_Action = to_diagonal_far} -> Player2_Left_Far	
	1: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

Player2_Right_Serve = pcase {
	9989: Low_Serve{player2_Last_Action = to_diagonal_near} -> Player1_Right_Near
	10: High_Serve{player2_Last_Action = to_diagonal_far} -> Player1_Right_Far	
	1: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

Player2_Left_Serve = pcase {
	9989: Low_Serve{player2_Last_Action = to_diagonal_near} -> Player1_Left_Near
	10: High_Serve{player2_Last_Action = to_diagonal_far} -> Player1_Left_Far	
	1: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

// Player 1 status
Player1_Left_Near = [player2_Last_Action == to_diagonal_near]from_Diagonal{player1_position = left} -> Player1_Counter_Diagonal_Near
					 []
					[player2_Last_Action == to_adjacent_near]from_Adjacent{player1_position = left} -> Player1_Counter_Adjacent_Near;

Player1_Left_Far = [player2_Last_Action == to_diagonal_far]from_Diagonal{player1_position = left} -> Player1_Counter_Diagonal_Far
					[]
				   [player2_Last_Action == to_adjacent_far]from_Adjacent{player1_position = left} -> Player1_Counter_Adjacent_Far;
				   
Player1_Right_Near = [player2_Last_Action == to_diagonal_near]from_Diagonal{player1_position = right} -> Player1_Counter_Diagonal_Near
					  []
					 [player2_Last_Action == to_adjacent_near]from_Adjacent{player1_position = right} -> Player1_Counter_Adjacent_Near;

Player1_Right_Far = [player2_Last_Action == to_diagonal_far]from_Diagonal{player1_position = right} -> Player1_Counter_Diagonal_Far
				   	 []
				    [player2_Last_Action == to_adjacent_far]from_Adjacent{player1_position = right} -> Player1_Counter_Adjacent_Far;
				    
// Player 1 make decision
Player1_Counter_Diagonal_Near = pcase {
	1: Go_Diagonal_Near -> Player1_Play_Diagonal_Near
	11: Go_Adjacent_Near -> Player1_Play_Adjacent_Near
	4: Go_Diagonal_Far -> Player1_Play_Diagonal_Far
	9: Go_Adjacent_Far -> Player1_Play_Adjacent_Far
	3: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

Player1_Counter_Adjacent_Near = pcase {
	3: Go_Diagonal_Near -> Player1_Play_Diagonal_Near
	15: Go_Adjacent_Near -> Player1_Play_Adjacent_Near
	7: Go_Diagonal_Far -> Player1_Play_Diagonal_Far
	9: Go_Adjacent_Far -> Player1_Play_Adjacent_Far
	4: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

Player1_Counter_Diagonal_Far = pcase {
	4: Go_Diagonal_Near -> Player1_Play_Diagonal_Near
	7: Go_Adjacent_Near -> Player1_Play_Adjacent_Near
	4: Go_Diagonal_Far -> Player1_Play_Diagonal_Far
	8: Go_Adjacent_Far -> Player1_Play_Adjacent_Far
	0: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

Player1_Counter_Adjacent_Far = pcase {
	11: Go_Diagonal_Near -> Player1_Play_Diagonal_Near
	26: Go_Adjacent_Near -> Player1_Play_Adjacent_Near
	10: Go_Diagonal_Far -> Player1_Play_Diagonal_Far
	25: Go_Adjacent_Far -> Player1_Play_Adjacent_Far
	1: Miss -> Player2_Get_Point{player2_Score = player2_Score + 1;serve_player = player2;} -> NewRound
};

// Player 1 actions
Player1_Play_Diagonal_Near = [player1_position == left]Player1_At_Left{player1_Last_Action = to_diagonal_near} -> Player2_Left_Near
							  []
							 [player1_position == right]Player1_At_Right{player1_Last_Action = to_diagonal_near} -> Player2_Right_Near;
							 
Player1_Play_Adjacent_Near = [player1_position == left]Player1_At_Left{player1_Last_Action = to_adjacent_near} -> Player2_Right_Near
							  []
							 [player1_position == right]Player1_At_Right{player1_Last_Action = to_adjacent_near} -> Player2_Left_Near;
							 
Player1_Play_Diagonal_Far = [player1_position == left]Player1_At_Left{player1_Last_Action = to_diagonal_far} -> Player2_Left_Far
							  []
							 [player1_position == right]Player1_At_Right{player1_Last_Action = to_diagonal_far} -> Player2_Right_Far;

Player1_Play_Adjacent_Far = [player1_position == left]Player1_At_Left{player1_Last_Action = to_adjacent_far} -> Player2_Right_Far
							  []
							 [player1_position == right]Player1_At_Right{player1_Last_Action = to_adjacent_far} -> Player2_Left_Far;

// Player 2 status
Player2_Left_Near = [player1_Last_Action == to_diagonal_near]from_Diagonal{player2_position = left} -> Player2_Counter_Diagonal_Near
					 []
					[player1_Last_Action == to_adjacent_near]from_Adjacent{player2_position = left} -> Player2_Counter_Adjacent_Near;

Player2_Left_Far = [player1_Last_Action == to_diagonal_far]from_Diagonal{player2_position = left} -> Player2_Counter_Diagonal_Far
					[]
				   [player1_Last_Action == to_adjacent_far]from_Adjacent{player2_position = left} -> Player2_Counter_Adjacent_Far;
				   
Player2_Right_Near = [player1_Last_Action == to_diagonal_near]from_Diagonal{player2_position = right} -> Player2_Counter_Diagonal_Near
					  []
					 [player1_Last_Action == to_adjacent_near]from_Adjacent{player2_position = right} -> Player2_Counter_Adjacent_Near;

Player2_Right_Far = [player1_Last_Action == to_diagonal_far]from_Diagonal{player2_position = right} -> Player2_Counter_Diagonal_Far
				   	 []
				    [player1_Last_Action == to_adjacent_far]from_Adjacent{player2_position = right} -> Player2_Counter_Adjacent_Far;
				    
// Player 2 make decision
Player2_Counter_Diagonal_Near = pcase {
	3: Go_Diagonal_Near -> Player2_Play_Diagonal_Near
	6: Go_Adjacent_Near -> Player2_Play_Adjacent_Near
	4: Go_Diagonal_Far -> Player2_Play_Diagonal_Far
	17: Go_Adjacent_Far -> Player2_Play_Adjacent_Far
	8: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

Player2_Counter_Adjacent_Near = pcase {
	6: Go_Diagonal_Near -> Player2_Play_Diagonal_Near
	16: Go_Adjacent_Near -> Player2_Play_Adjacent_Near
	8: Go_Diagonal_Far -> Player2_Play_Diagonal_Far
	23: Go_Adjacent_Far -> Player2_Play_Adjacent_Far
	6: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

Player2_Counter_Diagonal_Far = pcase {
	2: Go_Diagonal_Near -> Player2_Play_Diagonal_Near
	6: Go_Adjacent_Near -> Player2_Play_Adjacent_Near
	8: Go_Diagonal_Far -> Player2_Play_Diagonal_Far
	8: Go_Adjacent_Far -> Player2_Play_Adjacent_Far
	3: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

Player2_Counter_Adjacent_Far = pcase {
	9: Go_Diagonal_Near -> Player2_Play_Diagonal_Near
	10: Go_Adjacent_Near -> Player2_Play_Adjacent_Near
	3: Go_Diagonal_Far -> Player2_Play_Diagonal_Far
	25: Go_Adjacent_Far -> Player2_Play_Adjacent_Far
	4: Miss -> Player1_Get_Point{player1_Score = player1_Score + 1;serve_player = player1;} -> NewRound
};

// Player 2 actions
Player2_Play_Diagonal_Near = [player2_position == left]Player2_At_Left{player2_Last_Action = to_diagonal_near} -> Player1_Left_Near
							  []
							 [player2_position == right]Player2_At_Right{player2_Last_Action = to_diagonal_near} -> Player1_Right_Near;
							 
Player2_Play_Adjacent_Near = [player2_position == left]Player2_At_Left{player2_Last_Action = to_adjacent_near} -> Player1_Right_Near
							  []
							 [player2_position == right]Player2_At_Right{player2_Last_Action = to_adjacent_near} -> Player1_Left_Near;
							 
Player2_Play_Diagonal_Far = [player2_position == left]Player2_At_Left{player2_Last_Action = to_diagonal_far} -> Player1_Left_Far
							  []
							 [player2_position == right]Player2_At_Right{player2_Last_Action = to_diagonal_far} -> Player1_Right_Far;

Player2_Play_Adjacent_Far = [player2_position == left]Player2_At_Left{player2_Last_Action = to_adjacent_far} -> Player1_Right_Far
							  []
							 [player2_position == right]Player2_At_Right{player2_Last_Action = to_adjacent_far} -> Player1_Left_Far;


#define player1Win won == player1;
#define player2Win won == player2;
Game = WhoServe1st;NewRound;
#assert Game reaches player1Win with prob;
#assert Game reaches player2Win with prob;
#assert Game deadlockfree;